*** Settings ***
Resource  plone.txt

Suite setup  Setup
Suite teardown  Remove Document and Folder

Test setup  

*** Test cases ***


Content Menu
    [Tags]  action-menu
    Click Link  Home
    Click Link  An actionsmenu page
    Element Should Not Be Visible  css=a#delete
    Click Link  css=dl#plone-contentmenu-actions dt.actionMenuHeader a
    Element Should Be Visible  css=a#delete
    Click Link  css=dl#plone-contentmenu-actions dt.actionMenuHeader a
    Element Should Not Be Visible  css=a#delete

Switching Actions Menu
    [Tags]  action-menu
    Click Link  link=An actionsmenu page
    Element Should Not Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced
    Click Link  css=dl#plone-contentmenu-actions dt.actionMenuHeader a
    Element Should Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced
    Mouse Over  css=dl#plone-contentmenu-workflow dt.actionMenuHeader a
    Element Should Not Be Visible  css=a#delete
    Element Should Be Visible  css=a#advanced
    Mouse Over  css=dl#plone-contentmenu-actions dt.actionMenuHeader a
    Element Should Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced
    Click Link  css=dl#plone-contentmenu-actions dt.actionMenuHeader a
    Element Should Not Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced

Clicking outside of Actions Menu
    [Tags]  action-menu
    Click Link  link=An actionsmenu page
    Element Should Not Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced
    Click Link  css=dl#plone-contentmenu-actions dt.actionMenuHeader a
    Element Should Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced
    Mouse Down  css=h1
    Wait until keyword succeeds  10s  1s  Element Should Not Be Visible  css=a#delete
    Element Should Not Be Visible  css=a#advanced

Publish Document
    Click Link  link=An actionsmenu page
    Page Should Contain Element  css=dl#plone-contentmenu-workflow dt.actionMenuHeader a span.state-private
    Open Workflow Menu
    Click Link  link=Publish
    Page Should Contain Element  css=dl#plone-contentmenu-workflow dt.actionMenuHeader a span.state-published
    Open Workflow Menu
    Click Link  link=Send back
    Page Should Contain Element  css=dl#plone-contentmenu-workflow dt.actionMenuHeader a span.state-private

Copy Document
    Click Link  An actionsmenu page
    Open Action Menu
    Click Link  link=Copy
    Wait until keyword succeeds  10s  1s  Page Should Contain  An actionsmenu page copied.
    Click Link  An actionsmenu folder
    Open Action Menu
    Click Link  link=Paste
    Wait until keyword succeeds  10s  1s  Page Should Contain  Item(s) pasted.
    Click Link  link=Contents
    Xpath Should Match X Times  //table[@id = 'listing-table']/tbody/tr  1
    Click Button  Paste
    Xpath Should Match X Times  //table[@id = 'listing-table']/tbody/tr  2


*** Keywords ***


Setup  
    Log in  admin  admin
    Go to homepage
    Add Page  An actionsmenu page
    Go to homepage
    Create Folder

Create Folder
    Click Link  css=li#portaltab-index_html a
    Click Link  Folder
    Input Text  title  An actionsmenu folder
    Click Button  Save

Remove Document and Folder
    Remove Content  an-actionsmenu-page
    Remove Content  an-actionsmenu-folder

